apply plugin: 'maven'
apply plugin: 'signing'

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar, javadocJar
}

def envSecretKeyRingFile = System.getenv('SIGNING_KEY_RING')
if (envSecretKeyRingFile != null)
	ext.'signing.secretKeyRingFile' = envSecretKeyRingFile
def envKeyId = System.getenv('SIGNING_KEY_ID')
if (envKeyId != null)
	ext.'signing.keyId' = envKeyId
def envPassword = System.getenv('SIGNING_PASSWORD')
if (envPassword != null)
	ext.'signing.password' = envPassword

signing {
	required { gradle.taskGraph.hasTask(uploadArchives) }
	sign configurations.archives
}

def envSonatypeUser = System.getenv('SONATYPE_USER')
if (envSonatypeUser != null)
	ext.'sonatypeUserName' = envSonatypeUser
def envSonatypePassword = System.getenv('SONATYPE_PASSWORD')
if (envSonatypePassword != null)
	ext.'sonatypePassword' = envSonatypePassword


uploadArchives.repositories.mavenDeployer {
	beforeDeployment { deployment -> signing.signPom(deployment) }
	repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
		if (project.hasProperty('sonatypeUserName') && project.hasProperty('sonatypePassword')) {
			authentication(userName: sonatypeUserName, password: sonatypePassword)
		}
	}
	snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
		if (project.hasProperty('sonatypeUserName') && project.hasProperty('sonatypePassword')) {
			authentication(userName: sonatypeUserName, password: sonatypePassword)
		}
	}
	pom.project {
		packaging 'jar'
		url 'https://github.com/TypeFox/ls-api'
		licenses {
			license {
				name 'Eclipse Public License, Version 1.0'
				url 'http://www.eclipse.org/legal/epl-v10.html'
			}
		}
		developers {
			developer {
				name 'Sven Efftinge'
				email 'sven.efftinge@typefox.io'
				organization 'TypeFox'
				organizationUrl 'http://typefox.io'
			}
			developer {
				name 'Dennis Hübner'
				email 'dennis.huebner@typefox.io'
				organization 'TypeFox'
				organizationUrl 'http://typefox.io'
			}
			developer {
				name 'Anton Kosyakov'
				email 'anton.kosyakov@typefox.io'
				organization 'TypeFox'
				organizationUrl 'http://typefox.io'
			}
			developer {
				name 'Miro Spönemann'
				email 'miro.spoenemann@typefox.io'
				organization 'TypeFox'
				organizationUrl 'http://typefox.io'
			}
		}
		scm {
			connection 'scm:git:git@github.com:TypeFox/ls-api.git'
			developerConnection 'scm:git:git@github.com:TypeFox/ls-api.git'
			url 'git@github.com:TypeFox/ls-api.git'
		}
	}
}
