buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'org.xtext:xtext-gradle-plugin:1.0.5'
	}
}

ext.buildTime = new java.text.SimpleDateFormat('yyyyMMddHHmm').format(new Date())

task cleanLocalMavenRepo(type: Delete) {
  group = 'Repository'
  description = 'Delete the local Maven repository'
  delete 'build/maven-repository'
}

task cleanLocalP2Repo(type: Delete) {
  group = 'Repository'
  description = 'Delete the local P2 repository'
  delete 'build/p2-repository'
}

subprojects {
	ext.xtextVersion = '2.10.1-SNAPSHOT'
	ext.guavaVersion = '[14.0,19)'
	repositories {
		jcenter()
		maven {
			url 'https://oss.sonatype.org/content/repositories/snapshots'
		}
	}
	
	group = 'io.typefox.lsapi'
	
	version = '0.2.0-SNAPSHOT'
	
	apply plugin: 'java'
	apply plugin: 'org.xtext.xtend'
	apply plugin: 'eclipse'
	apply from: "${rootDir}/gradle/maven-deployment.gradle"
	apply from: "${rootDir}/gradle/manifest-gen.gradle"
	
	sourceCompatibility = '1.8'
	targetCompatibility = '1.8'

	xtend {
		debugger {
			sourceInstaller = 'PRIMARY'
			hideSyntheticVariables = false
		}
	}
	
	sourceSets {
		main.xtendOutputDir = 'src/main/xtend-gen'
		test.xtendOutputDir = 'src/test/xtend-gen'
	}
	
	javadoc {
		options.addStringOption('-Xdoclint:none')
	}
}

repositories {
  mavenCentral()
}

configurations {
  javaMaven
}

dependencies {
  // http://stackoverflow.com/questions/4206679/can-anyone-give-a-good-example-of-using-org-apache-maven-cli-mavencli-programatt
  javaMaven 'org.slf4j:slf4j-simple:1.7.21'
  javaMaven 'org.eclipse.aether:aether-connector-basic:1.1.0'
  javaMaven 'org.eclipse.aether:aether-transport-wagon:1.1.0'
  javaMaven 'org.apache.maven.wagon:wagon-http:2.10'
  javaMaven 'org.apache.maven.wagon:wagon-file:2.10'
  javaMaven 'org.apache.maven.wagon:wagon-http-lightweight:2.10'
  javaMaven 'org.apache.maven.wagon:wagon-provider-api:2.10'
  javaMaven 'org.apache.maven:maven-embedder:3.3.9'
}

task createLocalP2Repo(type: JavaExec) {
  group = 'Repository'
  description = 'Create a local P2 repository'
  dependsOn cleanLocalP2Repo,
    ":io.typefox.lsapi:createLocalMavenRepo",
    ":io.typefox.lsapi.annotations:createLocalMavenRepo",
    ":io.typefox.lsapi.services:createLocalMavenRepo"
  classpath configurations.javaMaven
  workingDir 'gradle/maven-tycho-p2-repository'
  main = 'org.apache.maven.cli.MavenCli'
  systemProperties = [
    'maven.multiModuleProjectDirectory': 'gradle/maven-tycho-p2-repository'
  ]
  args 'install'
}
