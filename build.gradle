buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'org.xtext:xtext-gradle-plugin:1.0.5'
	}
}

task cleanLocalMavenRepo(type: Delete) {
	group = 'Repository'
	description = 'Delete the local Maven repository'
	delete 'build/maven-repository'
}

ext.buildTime = new java.text.SimpleDateFormat('yyyyMMddHHmm').format(new Date())

subprojects {
	repositories {
		jcenter()
	}
	
	group = 'io.typefox.lsapi'
	
	apply plugin: 'java'
	apply plugin: 'org.xtext.xtend'
	apply plugin: 'eclipse'
	apply from: "${rootDir}/gradle/versions.gradle"
	apply from: "${rootDir}/gradle/java-compiler-settings.gradle"
	apply from: "${rootDir}/gradle/xtend-compiler-settings.gradle"
	apply from: "${rootDir}/gradle/maven-deployment.gradle"
	apply from: "${rootDir}/gradle/manifest-gen.gradle"
}

apply from: "${rootDir}/gradle/p2-deployment.gradle"

// generate an ide with xtend support
task ide(type: GradleBuild) {
	buildFile = 'ide/build.gradle'
	tasks = ['ide']
}
